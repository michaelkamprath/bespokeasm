

; Prime Numbers for the Minimal 64 Computer
;
;   Calculates and prints out all 32-bit prime numbers.
;
#require "slu4-min64-asm >= 1.2.0"

BUFFER_SIZE = 32

.org $8000

init:
    ldi 0xfe sta 0xffff         ; initialize stack pointer
start:
    cpy4ai _n_value,1
.n_loop:
    phsi 0
    phs4a _n_value
    jps is_prime32
    pls4
    pls cpi 1 beq .print_is_prime
    jpa .increment_n
.print_is_prime:
    phs4a _n_value jps print_value32 pls4
    phs2i is_prime_str jps _Print pls2
.increment_n:
    inc32a _n_value
    jpa .n_loop

_n_value: .4byte 0
is_prime_str: .cstr " is prime!\n"
is_not_prime_str: .cstr " is not prime\n"

; print_value32
;   prints the passed 4 byte value in decimal format
;
;   Arguments:
;       sp + 3 : 32-bit value to print (4 bytes) big endian
;
print_value32:
    phsi BUFFER_SIZE
    phs2i .buffer
    phs4s (3+3)
    jps uint32_to_decimal_cstr
    pls4
    pls2
    pls
    phs2i .buffer jps _Print pls2
    rts
.buffer: .zero BUFFER_SIZE

; is_prime32
;   determines wither the passed uint32 is a prime
;
;   Arguments
;       sp+3 : the value to determine if prime (4 bytes)
;       sp+7 : a place holder for return boolean
;
;   returns
;       sp+7 : 0 or 1 depending on whether N  is prime

_isquared_str: .cstr "I^2 = "
_nprefix_str: .cstr ", N = "
_eol_str: .cstr "\n"

is_prime32:
    ; first check for 2 or 3
    lds 3 cpi 0 bne .modulo_two        ; check top byte of N for 0
    lds 4 cpi 0 bne .modulo_two        ; check top byte of N for 0
    lds 5 cpi 0 bne .modulo_two        ; check top byte of N for 0
    lds 6 cpi 3 beq .is_prime          ; check N==3
    cpi 2 beq .is_prime                ; check N==2
    cpi 1 beq .is_not_prime            ; check N==1
    cpi 0 beq .is_not_prime            ; check N==0
.modulo_two:
    lds 6 lsr bcc .is_not_prime        ; see if N's least signficant bit is even or odd
.modulo_three:
    phs4i 3                           ; place divisor on stack
    phs4s (3+4)                       ; place dividend on stack (from stack)
    jps divide32
    pls4
    ; check if remainder is 0
    phs4i 0 jps compare32 pls4
    pls4
    beq .is_not_prime
.loop_init:
    cpy4ai .current_i_val,5
.loop:
    phs4a .current_i_val
    phs4a .current_i_val
    jps multiply_uint32
    ; high 4 bytes of result should be 0 since we are only doing 32 bit N
    lds 1 cpi 0 bne .iteration_loop_done
    lds 2 cpi 0 bne .iteration_loop_done
    lds 3 cpi 0 bne .iteration_loop_done
    lds 4 cpi 0 bne .iteration_loop_done
    pls4
    cpy4as .isquared,1
    pls4
    ; now compare low 4 bytes of I*I result to N
    phs4a .isquared phs4s 3+4 jps compare32 pls4 pls4
    ; if I*I > N, we are done
    bgt .loop_done_is_prime
.n_gte_i_squared:
    ; now check various modulos.
    ; check N % I == 0
    phs4a .current_i_val   ; I
    phs4s (3+4)            ; N
    jps divide32
    pls4                   ; quotient
    phs4i 0
    jps compare32
    pls4
    pls4
    beq .loop_done_is_not_prime
    ; check N % (I+2) == 0
    phs4a .current_i_val   ; I
    phs4i 2
    jps add32
    cpy4as .temp_val, 1
    pls4
    pls4
    phs4a .temp_val
    phs4s (3+4)
    jps divide32
    pls4                   ; quotient
    phs4i 0
    jps compare32
    pls4                   ; zero
    pls4                   ; remainder
    beq .loop_done_is_not_prime
    ; add 6 to I and loop
    phs4a .current_i_val
    phs4i 6
    jps add32
    cpy4as .current_i_val,1
    pls4
    pls4
    jpa .loop
.iteration_loop_done:
    ; get rid of I*I stack
    pls4
    pls4
.loop_done_is_prime:
.is_prime:
    ldi 1 sts 7
    rts
.loop_done_is_not_prime:
.is_not_prime:
    ldi 0 sts 7
    rts
.current_i_val: .4byte 0
.temp_val: .4byte 0

.isquared: .4byte 0

#include "math32lib.min64"
#include "stringlib.min64"
