; LED Chaser
;
; This program causes the LEDs on the KENBAK-1 to chase back and forth.
; It is written in a possibly "over done" manner to demonstrate some of
; the more unique programming techniques of the KENBAK-1, such as jump
; and mark.
;
#require "kenbak-1 >= 1.0.0"

DELAY_COUNT = 255

; CODE

init:
    ld a, 1
    ld [OUTPUT], a              ; init the screen display
.loop:
    jm left_loop                ; call left_loop function
    jm right_loop               ; call right_loop
    jp .loop                    ; restart loop

left_loop:
    .byte 0                     ; storage location for return address
.start:
    ld a, 2
    ld [OUTPUT], a              ; load OUT with starting value
.loop:
    jm delay                    ; jump to the delay fuction
    ld a, [OUTPUT]              ; grab the current display value
    sftl a, 1                   ; left shift A by 1
    jpz a, .end                 ; if A is zero, end this direction
    ld [OUTPUT], a              ; update display
    jp .loop                    ; restart loop
.end:
    jp [[left_loop]]

right_loop:
    .byte 0                     ; storage location for return address
.start:
    ld a, 64
    ld [OUTPUT], a              ; load OUT with starting value
.loop:
    jm delay                    ; jump to the delay fuction
    ld a, [OUTPUT]              ; grab the current display value
    sftr a, 1                   ; left right it by 1
    jpz a, .end                 ; if A is zero, end this direction
    ld [OUTPUT], a              ; update display
    jp .loop                    ; restart loop
.end:
    jp [[right_loop]]

delay:
    .byte 0                     ; storage location for return address
.start:
    ld b, DELAY_COUNT           ; load B with the delay count
.loop:
    sub b, 1                    ; subtract one from B
    jpnz b, .loop               ; if B is not zero, continue with loop
.end:
    jp [[delay]]                ; return to caller